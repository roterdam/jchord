# Copyright (c) 2008-2010, Intel Corporation.
# Copyright (c) 2006-2007, The Trustees of Stanford University.
# All rights reserved.
# Licensed under the terms of the New BSD License.

# Author: Mayur Naik (mhn@cs.stanford.edu)
# name=kobj-init-dlog

.include "I.dom"
.include "M.dom"
.include "K.dom"
.include "C.dom"
.include "H.dom"
.include "V.dom"

.bddvarorder I0xI1xI2xI3_M0xM1_C0xC1xC2xC3xC4_K0xH0xH1xH2xH3_V0xV1

###
# Relations
###

initCHC(c:C, h:H, c:C) input
truncCKC(c:C, k:K, c:C) input 	# K is the domain of integers
HK(h:H, k:K) input				#the parametric relation
roots(m:M) input
IM(i:I, m:M) input
VH(v:V, h:H) input
MI(m:M, i:I) input
MH(m:M, h:H) input
IinvkArg0(i:I,v:V1) input
statM(m:M) input

thisMV(m:M, v:V) input
HM(h:H, m:M)
CM_1(c:C, m:M)
CHC_1(c:C, h:H, c:C)
CHC_2(c:C, h:H, c:C)
CHC_3(c:C, h:H, c:C)

CM(c:C, m:M) output
CHC(c:C, h:H, c:C) output

###
# Constraints
###

HM(h,m) :- thisMV(m,v), VH(v,h).

#initialize CM for root methods with emptycontext
CM_1(0,m) :- roots(m).
#CM_1(c,m) :- CHC_1(_,h,c), IM(i,m), IinvkArg0(i,v), VH(v,h), !statM(m).
CM_1(c,m) :- CHC_1(_,h,c), HM(h,m).
CM_1(c,m1) :- CM_1(c,m0), IM(i,m1), statM(m1), MI(m0,i).

CHC_1(c1,h,c3) :- MH(m,h), CM_1(c1,m), HK(h,k), initCHC(c1,h,c2), truncCKC(c2,k,c3).
CHC_2(c1,h,c2) :- CHC_1(c1,h,c2), initCHC(c1,h,c2).
CHC_3(c1,h,c3) :- !CHC_2(c1,h,c3), CHC_1(c1,h,c3).

CM(0,m) :- roots(m).
#CM(c,m) :- CHC(_,h,c), IM(i,m), IinvkArg0(i,v), VH(v,h), !statM(m).
CM(c,m) :- CHC(_,h,c), HM(h,m).
CM(c,m1) :- CM(c,m0), IM(i,m1), statM(m1), MI(m0,i).

CHC(c1,h,c3) :- CHC_2(c1,h,c3).
CHC(c1,h,c3) :- MH(m,h), CM(c1,m), HK(h,k), initCHC(c1,h,c2), truncCKC(c2,k,c3), CHC_3(c1,h,c3).