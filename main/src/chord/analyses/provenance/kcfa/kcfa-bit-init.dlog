# Copyright (c) 2008-2010, Intel Corporation.
# Copyright (c) 2006-2007, The Trustees of Stanford University.
# All rights reserved.
# Licensed under the terms of the New BSD License.

# Author: Xin Zhang (xin.zhang@gatech.edu)
# name=kcfa-bit-init-dlog

.include "I.dom"
.include "M.dom"
.include "K.dom"
.include "C.dom"
.include "H.dom"

.bddvarorder I0xI1xI2xI3_M0xM1_C0xC1xC2xC3xC4_K0xH0xH1xH2xH3

###
# Relations
###

initCIC(c:C, i:I, c:C) input
initCHC(c:C, h:H, c:C) input
truncCKC(c:C, k:K, c:C) input 	# K is the domain of integers the parametric relation
IM(i:I, m:M) input
MI(m:M, i:I) input
MH(m:M, h:H) input
CL(c:C, k:K) input
AllowI(i:I, k:K) input
DenyI(i:I, k:K) input
AllowH(h:H, k:K) input
DenyH(h:H, k:K) input
roots(m:M) input

CIC_1(c:C, i:I, c:C)
CIC_2(c:C, i:I, c:C)

CHC_1(c:C, h:H, c:C)
CHC_2(c:C, h:H, c:C)

CM(c:C, m:M) output
CIC(c:C, i:I, c:C) output
CHC(c:C, h:H, c:C) output

###
# Constraints
###

CM(0,m) :- roots(m).
CM(c,m) :- CIC(_,i,c), IM(i,m).

AllowI(_,0).
AllowH(_,0).
AllowH(_,1).

CIC_1(c1,i,c2) :- MI(m,i),CM(c1,m),initCIC(c1,i,c2).
CIC(c1,i,c2) :- CIC_1(c1,i,c2),CL(c2,l),AllowI(i,l).

CIC_2(c1,i,c3) :- CIC_1(c1,i,c2), CL(c2,l1), DenyI(i,l2), truncCKC(c2,l2,c3), l2 <= l1.
CIC(c1,i,c2) :- CIC_2(c1,i,c2), CL(c2,l), AllowI(i,l).

CHC_1(c1,h,c2) :- MH(m,h), CM(c1,m), initCHC(c1,h,c2).
CHC(c1,h,c2) :- CHC_1(c1,h,c2),CL(c2,l),AllowH(h,l).

CHC_2(c1,h,c3) :- CHC_1(c1,h,c2), CL(c2,l1), DenyH(h,l2), truncCKC(c2,l2,c3), l2 <= l1.
CHC(c1,h,c2) :- CHC_2(c1,h,c2), CL(c2,l), AllowH(h,l).