# Copyright (c) 2008-2009, Intel Corporation.
# Copyright (c) 2006-2007, The Trustees of Stanford University.
# All rights reserved.

# 0-CFA-based may alias analysis with call graph construction

# Author: Mayur Naik (mhn@cs.stanford.edu)
# name=cipa-0cfa-dlog

.include "T.dom"
.include "F.dom"
.include "M.dom"
.include "I.dom"
.include "H.dom"
.include "V.dom"
.include "Z.dom"

# Relation HFH: 2.1157674E7 elements 
#F0_H0_H1: 54819 nodes
#F0_H1_H0: 121148 nodes
#H0_H1_F0: 137432 nodes
#H0_F0_H1: 21642 nodes
#H1_F0_H0: 23795 nodes
#H1_H0_F0: ?
#F0_H0xH1: 572534 nodes
#H0_F0xH1: 23795 nodes
#H1_F0xH0: 70483 nodes
#H0xH1_F0: ?
#F0xH1_H0: 150250 nodes
#F0xH0_H1: 73039 nodes
#H0xF0xH1: 3411490 nodes

.bddvarorder I0xM0xM1_T0_V0xV1_T1_H0_F0_H1_Z0

###
# Relations
###

VHfilter(v:V,h:H) input
HT(h:H0,t:T1) input
cha(n:M1,t:T1,m:M0) input
sub(t1:T1,t2:T0) input
MmethArg(m:M0,n:Z0,v:V0) input
MmethRet(m:M0,n:Z0,v:V1) input
IinvkArg0(i:I0,v:V1) input
IinvkArg(i:I0,n:Z0,v:V1) input
IinvkRet(i:I0,n:Z0,v:V0) input
MI(m:M,i:I) input
statIM(i:I,m:M) input
specIM(i:I,m:M) input
virtIM(i:I,m:M) input
MobjValAsgnInst(m:M,l:V,h:H) input
MobjVarAsgnInst(m:M,l:V,r:V) input
MgetInstFldInst(m:M,l:V,b:V,f:F) input
MputInstFldInst(m:M,b:V,f:F,r:V) input
MgetStatFldInst(m:M,l:V,f:F) input
MputStatFldInst(m:M,f:F,r:V) input
classT(t:T) input
staticTM(t:T,m:M) input
staticTF(t:T,f:F) input
clinitTM(t:T,m:M) input
reachableT(t:T) output
IHM(i:I,h:H,m:M)
VV(v1:V,v2:V)

VH(v:V,h:H) output
FH(f:F,h:H) output
HFH(h1:H,f:F,h2:H) output
rootM(m:M) output
reachableI(i:I) output
reachableM(m:M) output
IM(i:I,m:M) output
MM(m:M,n:M) output

specIMV(i:I,m:M,v:V)
objValAsgnInst(l:V,h:H) 
objVarAsgnInst(l:V,r:V) 
getInstFldInst(l:V,b:V,f:F) 
putInstFldInst(b:V,f:F,r:V) 
getStatFldInst(l:V,f:F) 
putStatFldInst(f:F,r:V) 

###
# Constraints
###

IHM(i,h,m2) :- virtIM(i,m1), HT(h,t), cha(m1,t,m2).

# base cases: each root method and each class initializer is reachable
reachableM(0).
reachableM(m) :- reachableT(t), clinitTM(t,m).
# inductive case
reachableM(m) :- IM(_,m).

reachableI(i) :- MI(m,i), reachableM(m).

# possibly-multiple-target call sites
IM(i,m) :- reachableI(i), IinvkArg0(i,v), VH(v,h), IHM(i,h,m).
# definitely-single-target call sites
specIMV(i,m,v) :- specIM(i,m), IinvkArg0(i,v).

IM(i,m) :- reachableI(i), specIMV(i,m,v), VH(v,_).
IM(i,m) :- reachableI(i), statIM(i,m).

# arguments and return/throw variables
VV(v,u) :- IinvkArg(i,z,u), IM(i,m), MmethArg(m,z,v).
VV(u,v) :- IinvkRet(i,z,u), IM(i,m), MmethRet(m,z,v).

VH(u,h) :- VV(u,v), VH(v,h), VHfilter(u,h).

objValAsgnInst(l,h)   :- reachableM(m), MobjValAsgnInst(m,l,h).
objVarAsgnInst(l,r)   :- reachableM(m), MobjVarAsgnInst(m,l,r).
getInstFldInst(l,b,f) :- reachableM(m), MgetInstFldInst(m,l,b,f).
putInstFldInst(b,f,r) :- reachableM(m), MputInstFldInst(m,b,f,r).
getStatFldInst(l,f)   :- reachableM(m), MgetStatFldInst(m,l,f).
putStatFldInst(f,r)   :- reachableM(m), MputStatFldInst(m,f,r).

VH(l,h) :- objValAsgnInst(l,h).
VH(l,h) :- objVarAsgnInst(l,r), VH(r,h), VHfilter(l,h).
VH(l,h2) :- getInstFldInst(l,b,f), VH(b,h1), HFH(h1,f,h2). split
HFH(h1,f,h2) :- putInstFldInst(b,f,r), VH(b,h1), VH(r,h2). split
VH(l,h) :- getStatFldInst(l,f), FH(f,h).
FH(f,h) :- putStatFldInst(f,r), VH(r,h).

## See: http://java.sun.com/docs/books/jls/second_edition/html/execution.doc.html
## Section 12.4 Initialization of Classes and Interfaces
## A class or interface type T will be initialized immediately before
## the first occurrence of any one of the following:

# 1. T is a class and an instance of T is created.
reachableT(t) :- reachableM(m), MobjValAsgnInst(m,_,h), HT(h,t).
# 2. T is a class and a static method declared by T is invoked
reachableT(t) :- reachableM(m), staticTM(t,m).
# 3. A static field declared by T is assigned. 
reachableT(t) :- reachableM(m), MputStatFldInst(m,f,_), staticTF(t,f).
# 4. A static field declared by T is used and the reference to the
#    field is not a compile-time constant.
reachableT(t) :- reachableM(m), MgetStatFldInst(m,_,f), staticTF(t,f).
# 5. Before a class is initialized, its direct superclass must be
#    initialized.
reachableT(t) :- classT(t), reachableT(s), sub(s,t).

rootM(0).
rootM(m) :- reachableT(t), clinitTM(t,m).
MM(m,n) :- IM(i,n), MI(m,i).

