# name=hybrid-thresc-dlog

.include "Q.dom"
.include "H.dom"
.include "F.dom"
.include "E.dom"
.include "P.dom"
.include "U.dom"

# H0xF0_H1
# H0xF0xH1
# H1_H0xF0
# bad
# F0_H0xH1
# H1_H0_F0
# H1_Q0_H0xF0

.bddvarorder E0_Q0xQ1xQ2_P0_U0xU1_H0xF0xH1

succ(p:Q,q:Q) input
asgnP(p:P0,l:U0) input
copyP(p:P0,l:U0,r:U1) input
copyQ(q:Q0,l:U0,r:U1) input
allocP(p:P0,l:U0,h:H0) input
getinstP(p:P0,l:U0,b:U1,f:F0) input
putinstP(p:P0,b:U0,f:F0,r:U1) input
getstatP(p:P0,l:U0) input
putstatP(p:P0,l:U0) input
spawnP(p:P0,v:U0) input
startP(p:P0,v:U0) input
basePU(p:P0,v:U0) input

asgnQ(q:Q0,l:U0) output
copyX(q:Q0,l:U0,r:U1) output
allocQ(q:Q0,l:U0,h:H0) output
getinstQ(q:Q0,l:U0,b:U1,f:F0) output
putinstQ(q:Q0,b:U0,f:F0,r:U1) output
getstatQ(q:Q0,l:U0) output
putstatQ(q:Q0,l:U0) output
spawnQ(q:Q0,v:U0) output
startQ(q:Q0,v:U0) output
baseQU(q:Q0,v:U0) output

relevantAllocQ(q:Q,l:U,h:H)
relevantCopyX(q:Q,l:U,r:U)
relevantGetinstQ(q:Q,l:U,b:U,f:F)
QHH_iTrans(q:Q,h1:H,h2:H)
presQQU(q1:Q,q2:Q,v:U)
presQU(q:Q0,v:U0) input
liveQU_o(q:Q0,v:U0) input
PQ(p:P0,q:Q0) input
PE(p:P0,e:E0) input
relevantP(p:P0) input
EU(e:E0,w:U0)

QHFH_i(q:Q0,h1:H0,f:F0,h2:H1) output
QUH_i(q:Q0,v:U0,h:H0) output
escQH_i(q:Q0,h:H0) output
escQHtc_i(q:Q0,h:H0) output
escQU(q:Q0,v:U0) output
hybridVisitedE(e:E0) output
hybridEscE(e:E0) output
queryE(e:E0) output
queryPU(p:P0,v:U0) output
queryQU(q:Q0,v:U0) output
relevantQUH(q:Q0,v:U0,h:H0) output
relevantPUH(p:P0,v:U0,h:H0) output
relevantEH(e:E0,h:H0) output
succTrans(p:Q,q:Q)

###

asgnQ(q,l) :- asgnP(p,l), PQ(p,q).
copyX(q,l,r) :- copyP(p,l,r), PQ(p,q).
copyX(q,l,r) :- copyQ(q,l,r).
allocQ(q,l,h) :- allocP(p,l,h), PQ(p,q).
getinstQ(q,l,b,f) :- getinstP(p,l,b,f), PQ(p,q).
putinstQ(q,b,f,r) :- putinstP(p,b,f,r), PQ(p,q).
getstatQ(q,l) :- getstatP(p,l), PQ(p,q).
putstatQ(q,l) :- putstatP(p,l), PQ(p,q).
spawnQ(q,v) :- spawnP(p,v), PQ(p,q).
startQ(q,v) :- startP(p,v), PQ(p,q).
baseQU(q,v) :- basePU(p,v), PQ(p,q).

###

succTrans(p,q) :- p=q.
succTrans(p,q) :- succ(p,q).
succTrans(p,r) :- succTrans(p,q), succTrans(q,r). 

QHFH_i(_,0,_,0).
escQH_i(_,0).

presQQU(q,q2,u) :- presQU(q,u), succ(q,q2).

QUH_i(q2,v,h) :- QUH_i(q,v,h), presQQU(q,q2,v). pri=5

relevantAllocQ(q,l,h) :- liveQU_o(q,l), allocQ(q,l,h).
relevantCopyX(q,l,r) :- liveQU_o(q,l), copyX(q,l,r).
relevantGetinstQ(q,l,b,f) :- liveQU_o(q,l), getinstQ(q,l,b,f).

QUH_i(q2,l,h) :- relevantAllocQ(q,l,h), succ(q,q2).
QUH_i(q2,l,h) :- relevantCopyX(q,l,r), QUH_i(q,r,h), succ(q,q2). split
QUH_i(q2,l,h) :- relevantGetinstQ(q,l,b,f), QUH_i(q,b,g), QHFH_i(p,g,f,h), succTrans(p,q), succ(q,q2). split
QHFH_i(q2,h1,f,h2) :- succ(q,q2), putinstQ(q,b,f,r), QUH_i(q,b,h1), QUH_i(q,r,h2). split
escQH_i(q2,h) :- putstatQ(q,l), QUH_i(q,l,h), succ(q,q2). 
escQH_i(q2,h) :- spawnQ(q,v), QUH_i(q,v,h), succ(q,q2).
QUH_i(q2,l,0) :- getstatQ(q,l), succ(q,q2).
QUH_i(q,v,0) :- startQ(q,v).

escQHtc_i(q2,h) :- escQH_i(q,h), succTrans(q,q2).
escQHtc_i(q,h) :- escQHtc_i(q,h1), QHFH_i(p,h1,_,h), succTrans(p,q).

escQU(q,v) :- QUH_i(q,v,h), escQHtc_i(q,h).

EU(e,w) :- basePU(p,w), PE(p,e), relevantP(p).

hybridVisitedE(e) :- PQ(p,_), PE(p,e), EU(e,_).
hybridEscE(e) :- escQU(q,v), PQ(p,q), PE(p,e), EU(e,v).

###

queryE(e) :- !hybridEscE(e), hybridVisitedE(e).

queryPU(p,v) :- PE(p,e), EU(e,v), queryE(e).
queryQU(q,v) :- queryPU(p,v), PQ(p,q).

QHH_iTrans(q,h,h2) :- QHFH_i(q,h,_,h2).
QHH_iTrans(q2,h,h2) :- QHH_iTrans(q,h,h2), succTrans(q,q2).

relevantQUH(q,v,h) :- queryQU(q,v), relevantQUH(q,v,h1), QHH_iTrans(q,h,h1).
relevantQUH(q,v,h) :- queryQU(q,v), QUH_i(q,v,h).
relevantPUH(p,v,h) :- relevantQUH(q,v,h), PQ(p,q).

relevantEH(e,h) :- PE(p,e), EU(e,v), relevantPUH(p,v,h).

