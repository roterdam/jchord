# name=hybrid-thresc-dlog

.include "V.dom"
.include "Q.dom"
.include "H.dom"
.include "F.dom"
.include "E.dom"
.include "P.dom"

.bddvarorder F0_E0_Q0xQ1_P0_V0xV1_H0xH1

succ(p:Q0,q:Q1) input
asgn(q:Q0,l:V0) input
copy(q:Q0,l:V0,r:V1) input
alloc(q:Q0,l:V0,h:H0) input
getinst(q:Q0,l:V0,b:V1,f:F0) input
putinst(q:Q0,b:V0,f:F0,r:V1) input
getstat(q:Q0,l:V0) input
putstat(q:Q0,l:V0) input
spawn(q:Q0,v:V0) input
start(q:Q0,v:V0) input

PQ(p:P0,q:Q0) input
PE(p:P0,e:E0) input
EV(e:E0,v:V0) input
flowinsEscE(e:E0) input

use(q:Q0,v:V0)
def(q:Q0,v:V0)
live_i(q:Q0,v:V0)
live_o(q:Q0,v:V0)
pres(q:Q0,v:V0) 

QHFH_i(q:Q0,h1:H0,f:F0,h2:H1)
QHFH_o(q:Q0,h1:H0,f:F0,h2:H1)
QVH_i(q:Q0,v:V0,h:H0)
QVH_o(q:Q0,v:V0,h:H0)
escQH_i(q:Q0,h:H0)
escQH_o(q:Q0,h:H0) 
escQHtc_i(q:Q0,h:H0)
escQV(q:Q0,v:V0) 
hybridVisitedE(e:E0) output
hybridEscE(e:E0) output

queryE(e:E0) output
queryPV(p:P0,v:V0)
queryQV(q:Q0,v:V0)
relevantQVH(q:Q0,v:V0,h:H0)
relevantPVH(p:P0,v:V0,h:H0)
relevantEH(e:E0,h:H0) output

###

def(q,l) :- asgn(q,l).
def(q,l) :- copy(q,l,_).
use(q,r) :- copy(q,_,r).
def(q,l) :- getinst(q,l,_,_).
use(q,b) :- getinst(q,_,b,_).
use(q,b) :- putinst(q,b,_,_).
use(q,r) :- putinst(q,_,_,r).
def(q,l) :- getstat(q,l).
use(q,r) :- putstat(q,r).
def(q,v) :- alloc(q,v,_).

live_o(p,v) :- live_i(q,v), succ(p,q).
live_i(p,v) :- live_o(p,v), !def(p,v).
live_i(p,v) :- use(p,v).

pres(q,v) :- live_o(q,v), !def(q,v).

###

QHFH_i(_,0,_,0).
QHFH_o(_,0,_,0).
escQH_i(_,0).
escQH_o(_,0).

QVH_o(q,v,h) :- QVH_i(q,v,h), pres(q,v).
QHFH_o(q,h1,f,h2) :- QHFH_i(q,h1,f,h2).
escQH_o(q,h) :- escQH_i(q,h).

QVH_i(q,v,h) :- QVH_o(p,v,h), succ(p,q).
QHFH_i(q,h1,f,h2) :- QHFH_o(p,h1,f,h2), succ(p,q).
escQH_i(q,h) :- escQH_o(p,h), succ(p,q).

QVH_o(q,l,h) :- alloc(q,l,h).
QVH_o(q,l,h) :- copy(q,l,r), QVH_i(q,r,h).
QVH_o(q,l,h) :- getinst(q,l,b,f), QVH_i(q,b,g), QHFH_i(q,g,f,h).
QHFH_o(q,h1,f,h2) :- putinst(q,b,f,r), QVH_i(q,b,h1), QVH_i(q,r,h2).
escQH_o(q,h) :- putstat(q,l), QVH_i(q,l,h).
QVH_o(q,l,0) :- getstat(q,l).
QVH_i(q,v,0) :- start(q,v).
QVH_o(q,v,0) :- spawn(q,v).

escQHtc_i(q,h) :- escQH_i(q,h).
escQHtc_i(q,h) :- escQHtc_i(q,h1), QHFH_i(q,h1,_,h).

escQV(q,v) :- QVH_i(q,v,h), escQHtc_i(q,h).

hybridVisitedE(e) :- PQ(p,_), PE(p,e), EV(e,_).
hybridEscE(e) :- escQV(q,v), PQ(p,q), PE(p,e), EV(e,v).

###

queryE(e) :- flowinsEscE(e), !hybridEscE(e), hybridVisitedE(e).
queryPV(p,v) :- PE(p,e), EV(e,v), queryE(e).
queryQV(q,v) :- queryPV(p,v), PQ(p,q).

relevantQVH(q,v,h) :- queryQV(q,v), relevantQVH(q,v,h1), QHFH_i(q,h,_,h1).
relevantQVH(q,v,h) :- queryQV(q,v), QVH_i(q,v,h).

relevantPVH(p,v,h) :- relevantQVH(q,v,h), PQ(p,q).
relevantEH(e,h) :- PE(p,e), EV(e,v), relevantPVH(p,v,h).

