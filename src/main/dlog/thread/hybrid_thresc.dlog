# name=hybrid-thresc-dlog

.include "Q.dom"
.include "H.dom"
.include "F.dom"
.include "E.dom"
.include "P.dom"
.include "U.dom"

.bddvarorder E0_Q0xQ1_P0_U0xU1_H0xH1xF0

succ(p:Q0,q:Q1) input
asgnP(p:P0,l:U0) input
copyP(p:P0,l:U0,r:U1) input
copyQ(q:Q0,l:U0,r:U1) input
allocP(p:P0,l:U0,h:H0) input
getinstP(p:P0,l:U0,b:U1,f:F0) input
putinstP(p:P0,b:U0,f:F0,r:U1) input
getstatP(p:P0,l:U0) input
putstatP(p:P0,l:U0) input
spawnP(p:P0,v:U0) input
startP(p:P0,v:U0) input
basePU(p:P0,v:U0) input

asgnQ(q:Q0,l:U0) output
copyX(q:Q0,l:U0,r:U1) output
allocQ(q:Q0,l:U0,h:H0) output
getinstQ(q:Q0,l:U0,b:U1,f:F0) output
putinstQ(q:Q0,b:U0,f:F0,r:U1) output
getstatQ(q:Q0,l:U0) output
putstatQ(q:Q0,l:U0) output
spawnQ(q:Q0,v:U0) output
startQ(q:Q0,v:U0) output
baseQU(q:Q0,v:U0) output

presQU(q:Q0,v:U0) input
liveQU_i(q:Q0,v:U0) input
liveQU_o(q:Q0,v:U0) input
PQ(p:P0,q:Q0) input
PE(p:P0,e:E0) input
relevantP(p:P0) input
EU(e:E0,w:U0)
#flowinsEscE(e:E0) input

QHFH_i(q:Q0,h1:H0,f:F0,h2:H1) output
QHFH_o(q:Q0,h1:H0,f:F0,h2:H1) output
QUH_i(q:Q0,v:U0,h:H0) output
QUH_o(q:Q0,v:U0,h:H0) output
escQH_i(q:Q0,h:H0) output
escQH_o(q:Q0,h:H0) output
escQHtc_i(q:Q0,h:H0) output
escQU(q:Q0,v:U0) output
hybridVisitedE(e:E0) output
hybridEscE(e:E0) output
queryE(e:E0) output
queryPU(p:P0,v:U0) output
queryQU(q:Q0,v:U0) output
relevantQUH(q:Q0,v:U0,h:H0) output
relevantPUH(p:P0,v:U0,h:H0) output
relevantEH(e:E0,h:H0) output

###

asgnQ(q,l) :- asgnP(p,l), PQ(p,q).
copyX(q,l,r) :- copyP(p,l,r), PQ(p,q).
copyX(q,l,r) :- copyQ(q,l,r).
allocQ(q,l,h) :- allocP(p,l,h), PQ(p,q).
getinstQ(q,l,b,f) :- getinstP(p,l,b,f), PQ(p,q).
putinstQ(q,b,f,r) :- putinstP(p,b,f,r), PQ(p,q).
getstatQ(q,l) :- getstatP(p,l), PQ(p,q).
putstatQ(q,l) :- putstatP(p,l), PQ(p,q).
spawnQ(q,v) :- spawnP(p,v), PQ(p,q).
startQ(q,v) :- startP(p,v), PQ(p,q).
baseQU(q,v) :- basePU(p,v), PQ(p,q).

###

QHFH_i(_,0,_,0).
QHFH_o(_,0,_,0).
escQH_i(_,0).
escQH_o(_,0).

QUH_o(q,v,h) :- QUH_i(q,v,h), presQU(q,v). split
QHFH_o(q,h1,f,h2) :- QHFH_i(q,h1,f,h2).
escQH_o(q,h) :- escQH_i(q,h).

#QUH_i(q,v,h) :- liveQU_i(q,v), QUH_o(p,v,h), succ(p,q).
QUH_i(q,v,h) :- QUH_o(p,v,h), succ(p,q).
QHFH_i(q,h1,f,h2) :- QHFH_o(p,h1,f,h2), succ(p,q). 
escQH_i(q,h) :- escQH_o(p,h), succ(p,q).

QUH_o(q,l,h) :- liveQU_o(q,l), allocQ(q,l,h).
QUH_o(q,l,h) :- liveQU_o(q,l), copyX(q,l,r), QUH_i(q,r,h). split
QUH_o(q,l,h) :- liveQU_o(q,l), getinstQ(q,l,b,f), QUH_i(q,b,g), QHFH_i(q,g,f,h). split
QHFH_o(q,h1,f,h2) :- putinstQ(q,b,f,r), QUH_i(q,b,h1), QUH_i(q,r,h2). split
escQH_o(q,h) :- putstatQ(q,l), QUH_i(q,l,h). 
escQH_o(q,h) :- spawnQ(q,v), QUH_i(q,v,h).
QUH_o(q,l,0) :- getstatQ(q,l).
QUH_i(q,v,0) :- startQ(q,v).

escQHtc_i(q,h) :- escQH_i(q,h).
escQHtc_i(q,h) :- escQHtc_i(q,h1), QHFH_i(q,h1,_,h).

escQU(q,v) :- QUH_i(q,v,h), escQHtc_i(q,h).

EU(e,w) :- basePU(p,w), PE(p,e), relevantP(p).

hybridVisitedE(e) :- PQ(p,_), PE(p,e), EU(e,_).
hybridEscE(e) :- escQU(q,v), PQ(p,q), PE(p,e), EU(e,v).

###

#queryE(e) :- flowinsEscE(e), !hybridEscE(e), hybridVisitedE(e).
queryE(e) :- !hybridEscE(e), hybridVisitedE(e).

queryPU(p,v) :- PE(p,e), EU(e,v), queryE(e).
queryQU(q,v) :- queryPU(p,v), PQ(p,q).

relevantQUH(q,v,h) :- queryQU(q,v), relevantQUH(q,v,h1), QHFH_i(q,h,_,h1).
relevantQUH(q,v,h) :- queryQU(q,v), QUH_i(q,v,h).
relevantPUH(p,v,h) :- relevantQUH(q,v,h), PQ(p,q).

relevantEH(e,h) :- PE(p,e), EU(e,v), relevantPUH(p,v,h).

