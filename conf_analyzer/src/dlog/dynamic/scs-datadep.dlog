#
#
#    Ari Rabkin
# name=scs-datadep-dlog


.include "V.dom"
.include "Opt.dom"
.include "I.dom"
.include "Z.dom"
.include "H.dom"
.include "M.dom"
.include "UV.dom"
.include "U.dom"
.include "M.dom"
.include "F.dom"

.bddvarorder I0_Z0_Opt0_M0_F0_V0_H0_H1_V1_V2_UV0_U0_U1

###
# Relations
###

MmethArg(m:M0,n:Z0,v:V0) input
MmethRet(m:M0,n:Z0,v:V1) input
IinvkArg(i:I0,n:Z0,v:V1) input
IinvkRet(i:I0,n:Z0,v:V0) input
IM(i:I,m:M) input
MI(m:M,i:I) input
MgetInstFldInst(m:M,l:V,b:V,f:F) input
MputInstFldInst(m:M,b:V,f:F,r:V) input
MgetStatFldInst(m:M,l:V,f:F) input
MputStatFldInst(m:M,f:F,r:V) input
VH(v:V,h:H) input
objVarAsgnInst(l:V,r:V) input

MprimDataDep(m:M,l:U0,r:U1) input
IinvkPrimArg(i:I0,n:Z0,u:U0) input
IinvkPrimRet(i:I0,n:Z0,u:U0) input
MgetStatFldPrimInst(m:M,l:U0,f:F) input
MputStatFldPrimInst(m:M,f:F,r:U0) input
MgetInstFldPrimInst(m:M,l:U,b:V,f:F) input
MputInstFldPrimInst(m:M,b:V,f:F,r:U) input

getInstFldInst(l:V,b:V,f:F)
getStatFldInst(l:V,f:F)

primRefDep(u:UV,v:V) input
primControlDep(i:I,u:UV) input 


statHF(f:F,site:Opt) input
instHF(b:H,f:F,site:Opt) input
refCdep(v:V,site:Opt) input
primCdep(u:U,site:Opt) input


APIMethod(i:I) input
returnsConf(i:I,v:V0,cst:Opt) input

confUVV(i:I, o:UV, in:V) input
OptNames(o:Opt,site:I) input
UVV(u:UV,v:V) input
UVU(uv:UV,u:U) input

FailurePath(i:I0) input

primConfDep(u:UV,cst:Opt)
getInstFldPrimInst(l:U0,b:V,f:F) 
getStatFldPrimInst(l:U,f:F) 

VVSCS(v:V0,u:V1) output
FailurePathM(m:M) output

refCdepSCS(v:V,cst:Opt) output
primCdepSCS(u:U,cst:Opt) output
failPathUFU(u:U,f:F,v:U) output
maybeUGet(u:U,cst:Opt)

cOnLine(i:I, cst:Opt) output
confControlDep(i:I, cst:Opt) output

refCdepSCS(v,name) :- confUVV(site,u,_),UVV(u,v),OptNames(name,site).
primCdepSCS(u,name) :- confUVV(site,uv,_),UVU(uv,u),OptNames(name,site).

FailurePathM(m) :- MI(m,i),FailurePath(i).

getInstFldInst(l,b,f) :- FailurePathM(m), MgetInstFldInst(m,l,b,f).
getStatFldInst(l,f)   :- FailurePathM(m), MgetStatFldInst(m,l,f).

getStatFldPrimInst(l,f)   :- FailurePathM(m), MgetStatFldPrimInst(m,l,f).
getInstFldPrimInst(l,b,f) :- FailurePathM(m), MgetInstFldPrimInst(m,l,b,f).

VVSCS(v,u) :- IinvkArg(i,z,u), FailurePath(i),IM(i,m), MmethArg(m,z,v).

failPathUFU(u,f,v) :- FailurePathM(m),MgetInstFldPrimInst(m,u,_,f),MputInstFldPrimInst(m,_,f,v).

maybeUGet(u,cst) :- getInstFldPrimInst(u,b,f), VH(b,h1), instHF(h1,f,cst).
maybeUGet(u,cst) :- getStatFldPrimInst(u,f), statHF(f,cst).

##FIXME: bunch more prim-handling cases needed
primDataDep(l,r) :- FailurePathM(m), MprimDataDep(m,l,r).
primCdepSCS(l,cst) :- primDataDep(l,r),primCdepSCS(r,cst).
primCdepSCS(u,cst) :- refCdepSCS(v,cst),IinvkPrimRet(i,_,u),IinvkArg(i,0,v),APIMethod(i).

primCdepSCS(v,cst) :- failPathUFU(v,_,u),primCdepSCS(u,cst).
primCdepSCS(v,cst) :- !failPathUFU(v,_,_),maybeUGet(v,cst).

refCdepSCS(l,cst) :- getInstFldInst(l,b,f), VH(b,h1), instHF(h1,f,cst). split
refCdepSCS(v,cst) :- getStatFldInst(v,f), statHF(f,cst).
refCdepSCS(v,cst) :- objVarAsgnInst(v,u),refCdepSCS(u,cst).
refCdepSCS(v,cst) :- refCdepSCS(u,cst),IinvkArg(i,_,u),IinvkRet(i,_,v),APIMethod(i).
refCdepSCS(v,cst) :- refCdepSCS(u,cst),IinvkArg(i,_,u),IinvkArg(i,0,v),APIMethod(i).
refCdepSCS(v,cst) :- VVSCS(v,u),refCdepSCS(u,cst).

refCdepSCS(v,cst) :- returnsConf(_,v,cst).
#,!FailurePath(i). 
