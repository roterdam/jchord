#
#
#    Ari Rabkin
# name=findconf-dlog

.include "I.dom"
.include "V.dom"
.include "Z.dom"
.include "M.dom"
.include "UV.dom"

.bddvarorder I0xZ0_Z1_V0_V1_M0_UV0_M1


rawConfUVV(i:I, o:UV, in:V) input
IM(i:I,m:M) input
IinvkArg(i:I0,n:Z0,v:V1) input
MmethArg(m:M0,n:Z0,u:V0) input
objVarAsgnInst(l:V,r:V) input
IinvkRetUV(i:I0,n:Z0,uv:UV0) input
MM(m:M0, m2:M1) input

argCopies(m:M0,n:Z0,u:V0)
confCallArg(i:I,v:V)
confWrapper(m:M, m2:M) output
confUVV(i:I,uv:UV,v:V) output
mConfArgs(m:M,z:Z) output

# (m,z,v) is in argCopies if v is a copy of the z'th argument to m.
argCopies(m,z,v)  :- MmethArg(m,z,v).
argCopies(m,z,v2) :- argCopies(m,z,v),objVarAsgnInst(v2,v).

# tuples (m,z) where arg z of method m is a config option name
mConfArgs(m,z) :- argCopies(m,z,v),confCallArg(_,v).

#(i,v) is a conf call arg where v is passed to a conf method at call site i
confCallArg(i,v) :- IM(i,m),mConfArgs(m,z),IinvkArg(i,z,v). split
confCallArg(i,v) :- rawConfUVV(i,_,v).

#m wraps m2 if m2 is a conf method and m calls it. This is slightly approximate.
confWrapper(m, m2) :- MM(m,m2), mConfArgs(m,_),mConfArgs(m2,_).

#If a method has a conf call argument, its return value is a conf return val
confUVV(i,uv,v) :- confCallArg(i,v),IinvkRetUV(i,0,uv).
#confUVV(0,0,0).   
#main's arg is a special conf value
