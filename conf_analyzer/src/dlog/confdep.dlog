#
#
#    Ari Rabkin
# name=confdep-dlog


.include "V.dom"
.include "I.dom"
.include "Z.dom"
.include "H.dom"
.include "M.dom"
.include "UV.dom"
.include "U.dom"
.include "Opt.dom"


.bddvarorder I0_I1_Z0_Opt0_M0_V0_H0_V1_V2_UV0_U0

###
# Relations
###


IinvkArg(i:I0,n:Z0,v:V1) input
IinvkPrimArg(i:I0,n:Z0,u:U0) input
refCdep(v:V,src:Opt) input
primCdep(u:U,src:Opt) input
cOnLine(i:I0, src:Opt) output
confControlDep(i:I0, src:Opt) output
primConfDep(u:UV,src:Opt)
primRefDep(u:UV,v:V) input
primControlDep(i:I,u:UV) input 
APIMethod(i:I) input
IM(i:I,m:M) input
UVU(uv:UV,u:U) input


#Needed to handle, e.g., dependence on pointers being non-null
primConfDep(u,opt) :- primRefDep(u,v),refCdep(v,opt).
confControlDep(i,opt) :- primConfDep(u,opt),primControlDep(i,u).

confControlDep(i,opt) :- primCdep(u,opt),UVU(uv,u),primControlDep(i,uv).

cOnLine(i,opt) :- IinvkArg(i,_,v),refCdep(v,opt).
cOnLine(i,opt) :- IinvkPrimArg(i,_,u),primCdep(u,opt).
cOnLine(i,opt) :- confControlDep(i,opt).

