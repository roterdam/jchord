# name=datadep-dlog

.include "T.dom"
.include "F.dom"
.include "M.dom"
.include "I.dom"
.include "H.dom"
.include "V.dom"
.include "Z.dom"
.include "StrConst.dom"
.include "E.dom"
.include "UV.dom"
.include "U.dom"

.bddvarorder I0xM0xM1_T0_V0xV1_E0_StrConst0_T1_H0_F0_H1_Z0xU0xU1xUV0


### Inputs from alias analysis or standard Java analyses
reachableM(m:M) input
VH(v:V,h:H) input
FH(f:F,h:H) input
IinvkArg0(i:I0,v:V1) input
IinvkArg(i:I0,n:Z0,v:V1) input
IinvkRet(i:I0,n:Z0,v:V0) input
IM(i:I,m:M) input
VV(v1:V,v2:V) input


objValAsgnInst(l:V,h:H) input
objVarAsgnInst(l:V,r:V) input
getInstFldInst(l:V,b:V,f:F) input
putInstFldInst(b:V,f:F,r:V) input
getStatFldInst(l:V,f:F) input
putStatFldInst(f:F,r:V) input
checkCastInst(l:V,t:T,r:V) input


#####Conf related
TaintArray(i:I,v:V,v2:V) input
Constructs(i:I,v:V) input
FromArray(e:E,v:V,v2:V) input
localMustAlias(v:V,u:V)
APIMethod(i:I) input
ImarkConf(h:H,v:UV,cst:StrConst) input
rconf(h:H,v:V,cst:StrConst) output
UVV(u:UV,v:V) input
UVU(uv:UV,u:U) input
cdep(v:V,h:H,cst:StrConst) output
cdepf(f:F,h:H,cst:StrConst) output
primcdep(u:U,h:H,cst:StrConst) output
HFC(h1:H,f:F,h2:H,cst:StrConst) output
#HFCU(h1:H,f:F,h2:H,cst:StrConst) output
returnsConf(i:I,v:V0,h:H,cst:StrConst) output
Mclones(m:M,l:V,i:I,r:V) input
clones(l:V,r:V)

#####Primitive tracking

MmethPrimArg(m:M0,n:Z0,u:U0) input
MmethPrimRet(m:M0,n:Z0,u:U0) input
IinvkPrimArg(i:I0,n:Z0,u:U0) input
IinvkPrimRet(i:I0,n:Z0,u:U0) input

MobjVarAsgnPrimInst(m:M,l:U0,r:U1) input
MprimDataDep(m:M,l:U0,r:U1) input


MgetStatFldPrimInst(m:M,l:U0,f:F) input
MputStatFldPrimInst(m:M,f:F,r:U0) input
MgetInstFldPrimInst(m:M,l:U,b:V,f:F) input
MputInstFldPrimInst(m:M,b:V,f:F,r:U) input

objVarAsgnPrimInst(l:U0,r:U1)
primDataDep(l:U0,r:U1)
getInstFldPrimInst(l:U0,b:V,f:F) 
putInstFldPrimInst(b:V,f:F,r:U) 
getStatFldPrimInst(l:U,f:F) 
putStatFldPrimInst(f:F,r:U) 
UcU(l:U,r:U)

####Prim tracking
objVarAsgnPrimInst(l,r) :- reachableM(m), MobjVarAsgnPrimInst(m,l,r).
primDataDep(l,r) :- reachableM(m), MprimDataDep(m,l,r).

getStatFldPrimInst(l,f)   :- reachableM(m), MgetStatFldPrimInst(m,l,f).
putStatFldPrimInst(f,r)   :- reachableM(m), MputStatFldPrimInst(m,f,r).

getInstFldPrimInst(l,b,f) :- reachableM(m), MgetInstFldPrimInst(m,l,b,f).
putInstFldPrimInst(b,f,r) :- reachableM(m), MputInstFldPrimInst(m,b,f,r).
clones(l,r) :- reachableM(m),Mclones(m,l,_,r).

UcU(v,u) :- IinvkPrimArg(i,z,u), IM(i,m), MmethPrimArg(m,z,v).
UcU(u,v) :- IinvkPrimRet(i,z,u), IM(i,m), MmethPrimRet(m,z,v).


#####Conf tracking

localMustAlias(v,u) :- objVarAsgnInst(u,v).
localMustAlias(v,u) :- objVarAsgnInst(v,u).

rconf(h,v,cst) :- ImarkConf(h,u,cst),UVV(u,v).

primcdep(u,h,cst) :- ImarkConf(h,uv,cst),UVU(uv,u).
primcdep(l,h,cst) :- primcdep(r,h,cst),objVarAsgnPrimInst(l,r).
primcdep(l,h,cst) :- UcU(l,r),primcdep(r,h,cst).
primcdep(l,h,cst) :- primDataDep(l,r),primcdep(r,h,cst).
primcdep(u,h,cst) :- cdep(v,h,cst),IinvkPrimRet(i,_,u),IinvkArg(i,0,v),APIMethod(i).
cdep(v,h,cst) :- primcdep(u,h,cst),IinvkPrimArg(i,_,u),IinvkRet(i,_,v),APIMethod(i).
cdep(v,h,cst) :- primcdep(u,h,cst),IinvkPrimArg(i,_,u),IinvkArg(i,0,v),APIMethod(i).

cdepf(f,h,cst) :- putStatFldPrimInst(f,r), primcdep(r,h,cst).
primcdep(u,h,cst) :- getStatFldPrimInst(u,f), cdepf(f,h,cst).
primcdep(l,h2,cst) :- getInstFldPrimInst(l,b,f), VH(b,h1), HFC(h1,f,h2,cst). split
HFC(h1,f,h2,cst) :- putInstFldPrimInst(b,f,r), VH(b,h1), primcdep(r,h2,cst). split

cdep(l,h2,cst) :- getInstFldInst(l,b,f), VH(b,h1), HFC(h1,f,h2,cst). split
HFC(h1,f,h2,cst) :- putInstFldInst(b,f,r), VH(b,h1), cdep(r,h2,cst). split
cdepf(f,h,cst) :- putStatFldInst(f,r), cdep(r,h,cst).
cdep(v,h,cst) :- getStatFldInst(v,f), cdepf(f,h,cst).
cdep(v,h,cst) :- rconf(h,v,cst).
cdep(v,h,cst) :- VH(v,h),rconf(h,_,cst).
cdep(v,h,cst) :- cdep(u,h,cst),IinvkArg(i,_,u),IinvkRet(i,_,v),APIMethod(i).
cdep(v,h,cst) :- cdep(u,h,cst),IinvkArg(i,_,u),IinvkArg(i,0,v),APIMethod(i).
cdep(v,h,cst) :- VV(v,u),cdep(u,h,cst).
cdep(v,h,cst) :- localMustAlias(v,u),cdep(u,h,cst).
cdep(v,h,cst) :- checkCastInst(v,_,u),cdep(u,h,cst).

#treat arrays and elements interchangeably
cdep(b,h,cst)  :- putInstFldInst(b,0,r), cdep(r,h,cst).
cdep(l,h,cst) :- getInstFldInst(l,b,0), cdep(b,h,cst).
cdep(v,h,cst) :- FromArray(_,v,u),cdep(u,h,cst).
cdep(v,h,cst) :- TaintArray(_,v,u),cdep(u,h,cst).
cdep(v,h,cst) :- clones(v,r),cdep(r,h,cst).
#cdep(v,h,cst) :- clones(v,r),HFC(h1,f,h2,cst).

returnsConf(i,v,h,cst) :- IinvkRet(i,_,v),cdep(v,h,cst),!APIMethod(i).