# Copyright (c) 2008-2009, Intel Corporation.
# Copyright (c) 2006-2007, The Trustees of Stanford University.
# All rights reserved.

# Author: Mayur Naik (mhn@cs.stanford.edu)
# name=datarace-prologue-dlog

.include "M.dom"
.include "C.dom"
.include "E.dom"
.include "A.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder F0_E0_A0_M0xM1xI0_C0xC1xC2

###
# Relations
###

writeE(e:E0) input
ME(m:M0,e:E0) input
EF(e:E0,f:F0) input
thrSenAbbrCICM(c:C0,i:I0,d:C1,m:M0) input
forkACM(t:A0,c:C0,m:M0) input
MI(m:M0,i:I0) input
initM(m:M0) input

reachableTCM(t:A0,c:C0,m:M0) output
reachableTCE(t:A0,c:C0,e:E0) output

rdOrWrTCEF(t:A0,c:C0,e:E0,f:F0) output
onlyWrTCEF(t:A0,c:C0,e:E0,f:F0) output

###
# Constraints
###

reachableTCM(t,c,m) :- forkACM(t,c,m).
reachableTCM(t,d,n) :- !initM(n), reachableTCM(t,c,m), MI(m,i), thrSenAbbrCICM(c,i,d,n).

reachableTCE(t,c,e) :- reachableTCM(t,c,m), ME(m,e).

rdOrWrTCEF(t,c,e,f) :- reachableTCE(t,c,e), EF(e,f).
onlyWrTCEF(t,c,e,f) :- reachableTCE(t,c,e), EF(e,f), writeE(e).
